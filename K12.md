# Hardcore Video Processing (1)

Vizzie Objekte vewenden "jit.gl.slab" oder "jit.gl.pix".
Sie sind die schnittstelle zwischen Jitter und GPU.

**vz.oper8r patch**

![](K12/invizzie.png)

## jit.gl.slab vs jit.gl.pix

- jit.gl.slab ... verwendet .jxs Datei. Man muss GLSL mit text schreiben.
- jit.gl.pix ... Man kann "gen" patch schreiben und jitter generierte mit dem gen Patch automatisch eine .jxs Datei

## jit.gl.slab 

## Beispiele

Es gibt viele .jxs Beispiele unter 

```
Max/Contents/Resources/C74/media/jitter/shaders
```

![](K12/examples.png)

## Schritt 1 : Scale

```
<jittershader name="default">
	<description> Default Slab </description>
	<param name="scale" type="float" default="1.0" />
	<param name="tex0" type="int" default="0" />
	<language name="glsl" version="1.0">
		<bind param="scale" program="fp" />
		<bind param="tex0" program="fp" />		
		<program name="vp" type="vertex">
<![CDATA[
varying vec2 texcoord;
void main (void)
{
    gl_Position = ftransform();
    texcoord    = vec2(gl_TextureMatrix[0] * gl_MultiTexCoord0);
}
]]>		
		</program>
		<program name="fp" type="fragment">
<![CDATA[
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform float scale;

void main(void) 
{
	gl_FragColor = texture2DRect(tex0, texcoord) * scale;
}
]]>
		</program>
	</language>
</jittershader>
```

- zwei Programme 
    - vertex
    - fragment
    
#### Fragment Shader
```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform float scale;

void main(void) 
{
	gl_FragColor = texture2DRect(tex0, texcoord) * scale;
}
```

- uniform ... eingegebenen Parameter mit Max Messages

![](K12/slab1.png)

## Schritt 2 : Monochrome

```
varying vec2 texcoord;
uniform sampler2DRect tex0;

void main(void) 
{
	vec4 color = texture2DRect(tex0, texcoord);
	float avg = (color.r + color.b + color.g) / 3.0;
	gl_FragColor = vec4(avg, avg, avg, 1.0);
}
```
![](K12/slab2.png)

## Schritt 3 : Color Control

jxs description
```
<jittershader name="default">
	<description> Default Slab </description>
	<param name="scale" type="vec3" default="1.0" />
	<param name="tex0" type="int" default="0" />
	<language name="glsl" version="1.0">
		<bind param="scale" program="fp" />
		<bind param="tex0" program="fp" />		
		<program name="vp" type="vertex">
```

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform vec3 scale;

void main(void) 
{
	vec4 color = texture2DRect(tex0, texcoord);
	gl_FragColor = vec4(color.r * scale.r, color.g * scale.g, color.b * scale.b, 1.0);
}
```

![](K12/slab3.png)

## Schritt 4 : Texcoord-Verarbeitung

jxs description
```
<jittershader name="default">
	<description> Default Slab </description>
	<param name="scale" type="float" default="50.0" />
	<param name="freq" type="float" default="0.1" />
	<param name="tex0" type="int" default="0" />
	<language name="glsl" version="1.0">
		<bind param="scale" program="fp" />
		<bind param="freq" program="fp" />
		<bind param="tex0" program="fp" />		
		<program name="vp" type="vertex">
```
fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform float scale;
uniform float freq;

void main(void) 
{
	vec2 distorted = texcoord;
	distorted.y += sin(distorted.x * freq) * scale; // sin function based on x pos
	gl_FragColor = texture2DRect(tex0, distorted);
}
]]>
```
![](K12/slab4.png)

## Schritt 5 : Blur

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform float scale;

void main(void) 
{
	vec4 center = texture2DRect(tex0, texcoord);
	vec4 above = texture2DRect(tex0, texcoord + vec2(0.0, scale));
	vec4 left = texture2DRect(tex0, texcoord + vec2(-scale, 0.0));
	vec4 right = texture2DRect(tex0, texcoord + vec2(scale, 0.0));
	vec4 bottom = texture2DRect(tex0, texcoord + vec2(0.0, -scale));
	
	gl_FragColor = (center + above+left+right+bottom) / 5.0;

}
```
![](K12/slab5.png)

## Schritt 6 : Blur2 (iterative operation)

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform int iterations;
uniform float scale;

void main(void) 
{
	vec4 accum = vec4(0.0);
	float offset = float(iterations) / 2.0;
	
	for(int i = 0; i < iterations; ++i){
		for(int j = 0; j < iterations; ++j){
			accum += texture2DRect(tex0, texcoord + vec2((float(i) - offset) * scale, (float(j) - offset) * scale));
		}
	}

	gl_FragColor = accum / float(iterations * iterations);

}
```

![](K12/slab6.png)

## Aufgabe 1

![](K12/assignment1.gif)

## Schritt 7 : Zwei Quellen

jxs description

```
<jittershader name="default">
	<description> Default Slab </description>
	<param name="tex0" type="int" default="0" />
	<param name="tex1" type="int" default="1" />
	<language name="glsl" version="1.0">
		<bind param="tex0" program="fp" />	
		<bind param="tex1" program="fp" />			
		<program name="vp" type="vertex">
```

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform sampler2DRect tex1;

void main(void) 
{
	vec4 colorFirst = texture2DRect(tex0, texcoord);
	vec4 colorSecond = texture2DRect(tex1, texcoord);

	gl_FragColor = max(colorFirst, colorSecond);
}
```

![](K12/slab7.png)


## Schritt 8 : Texture Prison

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform sampler2DRect tex1;

void main(void) 
{
	vec4 colorFirst = texture2DRect(tex0, texcoord);
	vec4 colorSecond = texture2DRect(tex1, texcoord);

	gl_FragColor = mod(texcoord.x, 200.0) < 120.0 && mod(texcoord.y, 200.0) < 120.0 ? colorFirst : colorSecond;
}
```

![](K12/slab8.png)

## Schritt 9 : Generativ
jxs description

```
<jittershader name="default">
	<description> Default Slab </description>
	<param name="freq" type="vec3" />
	<language name="glsl" version="1.0">
		<bind param="freq" program="fp" />
		<program name="vp" type="vertex">
```

fragment shader

```
varying vec2 texcoord;
uniform sampler2DRect tex0;
uniform sampler2DRect tex1;

void main(void) 
{
	vec4 colorFirst = texture2DRect(tex0, texcoord);
	vec4 colorSecond = texture2DRect(tex1, texcoord);

	gl_FragColor = mod(texcoord.x, 200.0) < 120.0 && mod(texcoord.y, 200.0) < 120.0 ? colorFirst : colorSecond;
}
```

![](K12/slab9.png)


